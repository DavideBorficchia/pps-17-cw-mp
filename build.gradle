group = projectGroup
version = projectVersion
description = projectDescription

def jarDestinationFolder = "${rootDir.path}$productionJarFolder"

def scalaStyleResPath = "${rootDir.path}$scalaStyleResFile"
def scalaStyleOutputPath = "${buildDir.path}$scalaStyleOutput"

allprojects {
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'com.dorongold.task-tree'

    repositories {
        // Repositories where to find libraries
        jcenter()
    }
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'scala'
    apply plugin: 'org.scoverage'
    apply plugin: 'com.github.maiflai.scalatest'
    apply plugin: 'scalaStyle'

    sourceCompatibility = "$jdkVersion"
    javadoc.destinationDir = file("$docsFolderJava/${project.name}")
    scaladoc.destinationDir = file("$docsFolderScala/${project.name}")

    dependencies {
        if (project.name != 'core')
            implementation project(':core')

        implementation 'org.scala-lang:scala-library:2.12.2'
        implementation 'com.typesafe.scala-logging:scala-logging_2.12:3.9.0'
        implementation 'io.vertx:vertx-lang-scala_2.12:3.5.2'

        if (project.name == 'core' || project.name == 'server' || project.parent.name == 'services') {
            implementation 'io.vertx:vertx-web-scala_2.12:3.5.2'
            implementation 'io.vertx:vertx-web-client-scala_2.12:3.5.2'
            implementation 'io.vertx:vertx-jdbc-client-scala_2.12:3.5.2'
            implementation 'io.vertx:vertx-mysql-postgresql-client:3.5.2'

            runtimeOnly 'mysql:mysql-connector-java:5.1.13'

            testRuntimeOnly 'org.hsqldb:hsqldb:2.4.1'
        }

        if (project.name != 'core-testing')
            testImplementation project(':core-testing')

        testImplementation 'org.scalatest:scalatest_2.12:3.0.1'

        testRuntime 'org.pegdown:pegdown:1.6.0'

        scoverage 'org.scoverage:scalac-scoverage-plugin_2.12:1.3.1'
        scoverage 'org.scoverage:scalac-scoverage-runtime_2.12:1.3.1'
    }

    if (project.name == 'client' || project.name == 'discovery' || project.name == 'rooms' || project.name == 'authentication') {
        apply plugin: 'com.github.johnrengelman.shadow'

        shadowJar {
            baseName = artifactId
            version = projectVersion
            classifier = project.name

            destinationDir = file(jarDestinationFolder)
            mergeServiceFiles('reference.conf') // solves problems with AKKA
        }
    }

    if (project.name == 'core' || project.parent.name == 'services') {
        // tests that use database, should run sequentially
        tasks.withType(Test) {
            maxParallelForks = 1
        }
    }

    scalaStyle {
        configLocation = scalaStyleResPath
        includeTestSourceDirectory = true
        source = "$scalaStyleSource"
        testSource = "$scalaStyleTestSource"
        outputFile = scalaStyleOutputPath
        failOnWarning = false
    }

    clean {
        // delete production folder
        delete(file(jarDestinationFolder))
        ant.delete dir: "latest-reports"
        ant.delete dir: "latest-docs"
    }

    reportScoverage.doLast {
        ant.move file: "${buildDir}/reports/scoverage",
                todir: "latest-reports/${project.name}"
    }

    build.finalizedBy scalaStyle
}

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "gradle.plugin.org.scoverage:gradle-scoverage:2.3.0"
        classpath "gradle.plugin.com.github.maiflai:gradle-scalatest:0.22"

        // Plug-in to show scalaStyle warnings
        // Usage: gradlew <project> scalaStyle
        classpath "org.github.ngbinh.scalastyle:gradle-scalastyle-plugin_2.11:1.0.1"

        // Plug-in to show task dependencies tree
        // Usage: gradlew <task 1>...<task N> taskTree
        classpath "gradle.plugin.com.dorongold.plugins:task-tree:1.3"

        // Plug-in to create executable jars
        // Usage: gradlew <project> shadowJar
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    }
}

defaultTasks 'clean', 'build', 'javadoc', 'scaladoc', 'reportScoverage'
